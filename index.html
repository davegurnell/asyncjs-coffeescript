<!doctype html>
<html lang="en">

  <head>
    <meta charset="utf-8">

    <title>Caffeine Hit</title>

    <meta name="description" content="Coffeescript talk for AsyncJS">
    <meta name="author" content="Dave Gurnell">

    <meta name="apple-mobile-web-app-capable" content="yes" />
    <meta name="apple-mobile-web-app-status-bar-style" content="black-translucent" />

    <meta name="viewport" content="width=device-width, initial-scale=1.0, maximum-scale=1.0, user-scalable=no">

    <link rel="stylesheet" href="css/reveal.css">
    <link rel="stylesheet" href="css/theme/coffee.css" id="theme">
    <link rel="stylesheet" href="css/talk.css">

    <!-- For syntax highlighting -->
    <link rel="stylesheet" href="lib/css/zenburn.css">

    <!-- If the query includes 'print-pdf', use the PDF print sheet -->
    <script>
      document.write( '<link rel="stylesheet" href="css/print/' + ( window.location.search.match( /print-pdf/gi ) ? 'pdf' : 'paper' ) + '.css" type="text/css" media="print">' );
    </script>

    <!--[if lt IE 9]>
    <script src="lib/js/html5shiv.js"></script>
    <![endif]-->
  </head>

  <body>

    <div class="reveal">

      <!-- Any section element inside of this container is displayed as a slide -->
      <div class="slides">

        <section data-markdown>
          <script type="text/template">
            # Caffeine Hit

            <img src="img/coffee-beans.jpg" alt="Mmmmm... coffee!">

            An exploration
            of [Coffeescript](http://coffeescript.org)
            by [@davegurnell](http://twitter.com/davegurnell)
            of [@mynaweb](http://twitter.com/mynaweb)

            <small>Image credit: Flickr user <a href="http://www.flickr.com/photos/puuikibeach/3299635718/">Puuikibeach</a></small>
          </script>
        </section>

        <section data-example data-js="no">
          <h2>Prologue 1</h2>
          <script type="text/template">

            # Coffeescript is a simple alternative syntax for
            # Javascript.

            # It's simple and terse:
            coffeescript =
              name:    'Coffeescript'
              creator: 'Jeremy Ashkenas'
              ofFame:  'Backbone.js and Underscore.js'
              website: 'http://coffeescript.org'
              github:  'https://github.com/jashkenas/coffee-script'

            # It makes functional programming (which you should care about) easy:
            fib = (n) ->
              if n <= 2 then 1 else fib(n-1) + fib(n-2)

            # And it makes coding faster and less error-prone:
            [ a, b, c ] = [ 1, 2, 3, ]

          </script>
        </section>

        <section data-example data-js="no">
          <h2>Prologue 2</h2>
          <script type="text/template">

            # About me:
            speaker =
              name:    'Dave Gurnell'
              twitter: '@davegurnell'
              website: 'http://davegurnell.com'

            # I develop full-stack web applications in all kinds of tech:
            #   design, architect, build, test, train, etc...

            # I recently co-founded a startup doing A/B testing for web and mobile:
            founderOf =
              name:    'Myna'
              website: 'http://mynaweb.com'

            # I'm a Scorpio and my favorite programming is functional:
            preferredLanguages =
              server: 'Scala'
              client: 'Coffeescript'

            # ...and I'm available for hire!!!
          </script>
        </section>

        <section data-markdown>
          ## The agenda

          - Introduction to syntax
          - Important features
          - Important gotchas
          - Build tools and debugging
        </section>

        <section data-markdown>
          ## Following along at home...

          - **Arrow keys** switch slides
          - **C** focuses on a Coffeescript editor
          - **J** focuses on a Javascript editor
          - **Esc** defocuses the current editor
          - **Ctrl-E** or **Cmd-E** evaluates the script
          - **Ctrl-/** or **Cmd-/** (un)comments code
        </section>

        <section data-markdown>
          <script type="text/template">
            ## Part 1: Hello World

            <img src="img/hello-world.jpg" alt="Hello world">

            In which we meet our hero and have some fun with syntax

            <small>Image credit: <a href="http://www.flickr.com/photos/oskay/472097903/">Windell Oskay</a></small>
          </script>
        </section>

        <!--
        - Variables
        - Conditionals
        - Loops and comprehensions
        - Functions
          -
        -->

        <section data-example>
          <h2>Variables 1</h2>
          <script type="text/template">
            # Coffeescript auto-declares variables for you

            name = "Dave"
            age = 34
            hacker = true

            arr = [ 1, 2, 3 ]
            obj = { name: "Dave", age: 34 }

            greet = (name) ->
              "Hello #{name}"
          </script>
        </section>

        <section data-example>
          <h2>Conditionals</h2>
          <script type="text/template">
            # Standard conditional
            if positiveThing
              console.log "Win!"

            # Negative conditional
            #unless negativeThing
            #  console.log "Win again!"

            # Postfix notation
            #console.log("woo!") if 2 * 2 == 4
            #console.log("noo!") unless 2 * 2 == 4
          </script>
        </section>

        <section data-example>
          <h2>Loops and comprehensions 1</h2>
          <script type="text/template">
            # Conventional "for" loops involve ranges:

            # Inclusive range:
            for i in [1..10]
              console.log(i)

            # Exclusive range:
            #for i in [1...10]
            #  console.log(i)

            # In steps:
            #for i in [1...10] by 2
            #  console.log(i)

            # In reverse:
            #for i in [10..1]
            #  console.log(i)
          </script>
        </section>

        <section data-example>
          <h2>Loops and comprehensions 2</h2>
          <script type="text/template">
            # Arrays:
            for value in array
              console.log value

            # Arrays with indices:
            for value, index in array
              console.log "#{index}: #{value}"

            # Objects with keys and values:
            for key, value of object
              console.log "#{key}: #{value}"
          </script>
        </section>

        <section data-example>
          <h2>Loops and comprehensions 3</h2>
          <script type="text/template">
            # Conditionals:
            for value in array when value % 2 == 0
              console.log value
          </script>
        </section>

        <section data-example>
          <h2>Functions</h2>
          <script type="text/template">
            empty = ->

            returnFoo = -> "foo"

            multiply = (a, b) ->
              a * b

            divide = (a, b = 1) ->
              a / b

            add = (args...) ->
              _.reduce(args, (a, b) -> a + b, 0)

            add 1, 2, 3, 4, 5

            add array...
          </script>
        </section>

        <section data-markdown>
          <script type="text/template">
            ## Part 2: Little treasures

            <img src="img/treasure-island.jpg" alt="Treasure island">

            In which we dig deep into some indispensable features

            <small>Image credit: Flickr user <a href="http://www.flickr.com/photos/adplayers/7269497292/">Adplayers</a></small>
          </script>
        </section>

        <section data-markdown>
          ### Feature #1

          Everything is an expression
        </section>

        <section data-markdown>
          ## Expressions

          - Everything is an expression
          - Examples
          - Judicious use of return
        </section>

        <section data-markdown>
          ### Feature #2

          The existential operator
        </section>

        <section data-markdown>
          ## Existential operator

          - Infix use
          - Suffix use
          - "?=" use
        </section>

        <section data-markdown>
          ### Feature #3

          "Fat arrow"
        </section>

        <section data-markdown>
          ## Fat arrow

          - Short function syntax = easier FP
          - Fat arrow
          - Errors using fat arrow
        </section>

        <section data-markdown>
          ### Feature 4

          Classes and objects
        </section>

        <section data-markdown>
          ## Classes and objects

          - Short function syntax = easier FP
          - Fat arrow
          - Errors using fat arrow
        </section>

        <section data-markdown>
          <script type="text/template">
            ## Part 3: Eeeeevil!

            In which we examine potential gotchas

            <img src="img/nosferatu.jpg" alt="Eeeeevil">
          </script>
        </section>

        <section data-example>
          <h2>Variable shadowing</h2>
          <script type="text/template">
            # Declarations are hoisted to
            # the top of the current block

            name = "Dave"

            greetDave = () ->
              greeting = "Hello " + name
              console.log(greeting)

            # However, it is impossible to
            # shadow an outer variable declaration

            greetEveryone = () ->
              name = "everyone"
              greeting = "Hello " + name
              console.log(greeting)

            greetEveryone() # name set to "everyone"

            greetDave() # oh noes!
          </script>
        </section>
      </div>
    </div>

    <script src="lib/js/jquery.js"></script>
    <script src="lib/js/underscore.js"></script>
    <script src="lib/js/coffeescript.js"></script>
    <script src="lib/js/ace/ace.js"></script>

    <script src="lib/js/head.min.js"></script>
    <script src="js/reveal.js"></script>

    <script type="text/coffeescript">
      do () ->
        whitespaceChars = [ ' ', '\t' ]

        isBlank = (line) ->
          /^[ \t]+$/.test(line)

        lineIndent = (line) ->
          match = line.match /^[ \t]*[^ \t]/
          if match then match[0].length - 1 else 1000

        trim = (script) ->
          script.replace(/^([ \t]*[\r\n])/, '')
                .replace(/([\r\n][ \t]*)$/, '')

        reindent = (script) ->
          lines  = script.split(/[\r\n]/)
          indent = _.min(_.map(lines, lineIndent))
          _.map(lines, (line) -> line.substring(indent)).join('\n')

        evaluate = (script, mode) ->
          console.clear()
          ans = eval switch mode
                 when 'coffee' then CoffeeScript.compile(reindent(trim(script)))
                 else "(function() { #{trim(script)} })();"
          return

        initEditor = (parent, script, mode, theme = 'ace/theme/twilight') ->
          self = $(this)

          pane = $('<div>')
            .html(script)
            .appendTo(parent)
            .addClass("editor-pane #{mode}-editor-pane")

          editor = ace.edit(pane.get(0))
          editor.setTheme(theme)

          session = editor.getSession()
          session.setMode switch mode
                            when 'coffee' then 'ace/mode/coffee'
                            else 'ace/mode/javascript'
          session.setUseSoftTabs true
          session.setUseWrapMode true
          session.setTabSize 2

          editor.commands.addCommand
            name: 'Eval'
            bindKey: { win: 'Ctrl-E', mac: 'Command-E' },
            exec: () -> evaluate(session.getValue(), mode)

          editor.commands.addCommand
            name: 'Blur'
            bindKey: { win: 'Escape', mac: 'Escape' },
            exec: () -> editor.blur()

          pane.data('editor', editor)

          { pane, editor, session }

        initEvalButton = (parent, mode) ->
          $('<button class="eval">')
            .data('mode', mode)
            .text(if /Mac/g.test(navigator.platform) then 'Eval (Cmd-E)' else 'Eval (Ctrl-E)')
            .appendTo(parent)

        evalButtonClicked = (evt) ->
          button  = $(this)
          mode    = button.data('mode')
          wrapper = button.parents('.editor-panes')
          editor  = wrapper.find(".editor-pane-#{mode}").data('editor')
          evaluate(editor.getSession().getValue(), mode)

        # Before we show any slides, we add a div.editor-panes
        # to fix their size. This prevents visual glitches when
        # we change slides.
        initExample = () ->
          self = $(this)

          unless self.is('[data-example]')
            return

          wrapper = $('<div>')
            .addClass('editor-panes')
            .appendTo(self)

        showExample = () ->
          self = $(this)

          unless self.is('[data-example]')
            return

          showCoffee = ! _.contains ['no', 'false'], self.data('coffee')
          showJs     = ! _.contains ['no', 'false'], self.data('js')
          script = reindent(trim(self.find('script').html()))

          wrapper = self.find('.editor-panes')

          if showCoffee && showJs
            coffee  = initEditor(wrapper, script, 'coffee')
            js      = initEditor(wrapper, '', 'js')
            run     = initEvalButton(wrapper, 'coffee')

            sync = () ->
              try
                js.session.setValue(trim(
                  CoffeeScript.compile(coffee.session.getValue(), bare: true)))
              catch { message }
                js.session.setValue message

            sync()

            coffee.session.on 'change', sync

          else if showCoffee
            coffee = initEditor(wrapper, script, 'coffee')
            run    = initEvalButton(wrapper, 'coffee')
            wrapper.addClass("full-width")
            coffee.editor.resize()

          else if showJs
            js  = initEditor(wrapper, script, 'js')
            run = initEvalButton(wrapper, 'js')
            wrapper.addClass("full-width")
            js.editor.resize()

          else

          return

        hideExample = () ->
          slide = $(this)
          slide.find('.editor-pane').each () -> $(this).data('editor')?.destroy()
          slide.find('.editor-panes').html('')

        # Global keyup handler
        globalKeyUp = (evt) ->
          coffeeEditor = $('section.present .coffee-editor-pane').data('editor')
          jsEditor     = $('section.present .js-editor-pane').data('editor')
          unless coffeeEditor?.isFocused() || jsEditor?.isFocused()
            switch evt.keyCode
              when 67 # 'c'
                coffeeEditor?.focus()
                jsEditor?.blur()
              when 74 # 'j'
                coffeeEditor?.blur()
                jsEditor?.focus()

        # Slide change handler for reveal.js:
        slideChanged = (evt) ->
          showExample.call(evt.currentSlide)
          hideExample.call(evt.previousSlide)
          console.clear()

        $('body')
          .on('keyup', globalKeyUp)
          .on('click', 'button.eval', evalButtonClicked)
          .find('[data-example]').each(initExample)

        Reveal.initialize
          controls: true
          progress: true
          history: true
          center: true
          rollingLinks: false

          theme: Reveal.getQueryHash().theme
          transition: 'none'

          # Optional libraries used to extend on reveal.js
          dependencies: [
            {
              src: 'lib/js/classList.js'
              condition: () => !document.body.classList
            }
            {
              src: 'plugin/markdown/showdown.js'
              condition: () => !!document.querySelector('[data-markdown]')
            }
            {
              src: 'plugin/markdown/markdown.js'
              condition: () => !!document.querySelector('[data-markdown]')
            }
            {
              src: 'plugin/highlight/highlight.js'
              async: true
              callback: () => hljs.initHighlightingOnLoad()
            }
            # {
            #   src: 'plugin/zoom-js/zoom.js'
            #   async: true
            #   condition: () => !!document.body.classList
            # }
            # {
            #   src: 'plugin/notes/notes.js'
            #   async: true
            #   condition: () => !!document.body.classList
            # }
            # {
            #   src: 'plugin/search/search.js'
            #   async: true
            #   condition: () => !!document.body.classList
            # }
            # {
            #   src: 'plugin/remotes/remotes.js'
            #   async: true
            #   condition: () => !!document.body.classList
            # }
          ]

        Reveal.addEventListener 'slidechanged', slideChanged
    </script>

  </body>
</html>
